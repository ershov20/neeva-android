plugins {
    id 'com.android.application'
    id 'com.apollographql.apollo3' version "$apollo_version"
    id 'com.google.android.gms.oss-licenses-plugin'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'org.jetbrains.kotlin.android'
    id 'org.jlleitschuh.gradle.ktlint' version "$ktlint_version"
}

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.neeva.app"
        testApplicationId "com.neeva.app.test"
        namespace "com.neeva.app"

        minSdk 28
        targetSdk 31
        versionCode 94
        versionName "0.70.0"

        vectorDrawables {
            useSupportLibrary true
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Make the Android Test Orchestrator clear the app's state between test runs to avoid
        // having tests interfere with each other.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        testOptions {
            execution 'ANDROIDX_TEST_ORCHESTRATOR'

            // Make sure that the unit tests can get access to whatever resources they need.
            unitTests {
                includeAndroidResources = true
            }
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                    "room.schemaLocation": "$projectDir/schemas".toString(),
                    "room.incremental": "true"
                ]
            }
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        staging {
            initWith release
            applicationIdSuffix '.staging'
            versionNameSuffix '-STAGING'
            signingConfig signingConfigs.debug
        }
    }

    kapt {
        arguments {
            // Allow Room to output database schemas for every version to make it possible to
            // migrate from one to the next more easily.
            arg("room.schemaLocation", "$projectDir/schemas")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
        freeCompilerArgs = [
            // Needed for the experimental Compose functions
            '-Xopt-in=kotlin.RequiresOptIn'
        ]
    }

    buildFeatures {
        compose true
    }

    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }

    apollo {
        generateKotlinModels.set(true)
        packageName.set("com.neeva.app")
    }

    // Ignore a bunch of files with the same name that libraries may include but are completely
    // useless for the APK.
    packagingOptions {
        resources {
            excludes += [
                'META-INF/AL2.0',
                'META-INF/LGPL2.1',
                'META-INF/LICENSE',
                'META-INF/LICENSE.txt',
                'META-INF/NOTICE'
            ]
        }
    }

    // Chromium expects to be able to mmap these assets.
    androidResources {
        noCompress 'dat', 'bin', 'pak', 'proto'
    }

    sourceSets {
        findByName("main")?.java?.srcDirs(project.file("src/main/kotlin"))

        androidTest {
            // Allow the instrumentation tests to get access to the Room DB schemas.
            assets.srcDirs += files("$projectDir/schemas".toString())
        }
    }
}

dependencies {
    implementation project(path: ':weblayer')
    implementation project(path: ':chromium:gen:org__chromium__weblayer__support')

    implementation 'androidx.activity:activity-compose:1.4.0'
    implementation "androidx.activity:activity-ktx:$androidx_activity_version"
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.browser:browser:1.4.0'
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.material3:material3:1.0.0-alpha05"
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    implementation "androidx.constraintlayout:constraintlayout-compose:1.0.0"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.navigation:navigation-compose:$navigation_compose_version"
    implementation 'androidx.security:security-crypto-ktx:1.1.0-alpha03'
    implementation "androidx.preference:preference-ktx:$androidx_preference_version"
    implementation 'androidx.window:window:1.0.0'
    implementation "com.apollographql.apollo3:apollo-runtime:$apollo_version"
    implementation "com.google.accompanist:accompanist-navigation-animation:$accompanist_version"
    implementation "com.google.android.material:material:$google_android_material_version"
    implementation "com.google.android.gms:play-services-auth:$play_services_auth_version"
    implementation "com.google.android.gms:play-services-base:$play_services_version"
    implementation "com.google.android.gms:play-services-oss-licenses:17.0.0"
    implementation 'com.google.android.play:core-ktx:1.8.1'
    implementation "io.coil-kt:coil-compose:$coil_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$kotlin_coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$kotlin_coroutines_version"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.9.1'

    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.7.3'
    testImplementation "io.strikt:strikt-core:$strikt_version"
    testImplementation 'org.mockito.kotlin:mockito-kotlin:4.0.0'
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlin_coroutines_version"

    androidTestImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:$kotlin_coroutines_version"
    androidTestImplementation "io.strikt:strikt-core:$strikt_version"
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"
    androidTestUtil 'androidx.test:orchestrator:1.4.1'

    // Compose instrumentation testing requires we add the dependency to debugImplementation so that
    // the tests can start a ComponentActivity without crashing.
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_version"

    // Hilt
    kapt "com.google.dagger:hilt-compiler:$dagger_version"
    implementation "com.google.dagger:hilt-android:$dagger_version"

    kaptTest "com.google.dagger:hilt-compiler:$dagger_version"
    testImplementation "com.google.dagger:hilt-android-testing:$dagger_version"

    kaptAndroidTest "com.google.dagger:hilt-compiler:$dagger_version"
    androidTestImplementation "com.google.dagger:hilt-android-testing:$dagger_version"

    // Moshi
    kapt "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"

    // Room
    annotationProcessor "androidx.room:room-compiler:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-paging:$room_version"

    testImplementation "androidx.room:room-testing:$room_version"
    androidTestImplementation "androidx.room:room-testing:$room_version"

    // Paging
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"
    testImplementation "androidx.paging:paging-common-ktx:$paging_version"
    implementation "androidx.paging:paging-compose:1.0.0-alpha14"
}

hilt {
    enableAggregatingTask = true
}

kapt {
    correctErrorTypes true
}

// Output the status of test runners to the log.
tasks.withType(Test) {
    testLogging {
        exceptionFormat "full"
        events "started", "skipped", "passed", "failed"
        showStandardStreams true
        showCauses true
        showExceptions true
        showStackTraces true
    }
}

// Set up ktlint.
ktlint {
    android.set(true)
    outputColorName.set("RED")
}
